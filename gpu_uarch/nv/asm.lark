//
// Copyright © 2019 Paweł Dziepak
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in
// all copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.

NUMBER_Z: /(\d+|Z)/
NUMBER_T: /(\d+|T)/
NUMBER_OPT: /(\-+|\d+)/
YIELD_OPT: /[-Y]/
IDENTIFIER: /\S+/
DIGIT: /\d/
DECIMAL: /\d+/
HEXDECIMAL: /0x[\da-fA-F]+/

control: NUMBER_OPT ":" NUMBER_OPT ":" NUMBER_OPT ":" YIELD_OPT ":" /[\da-fA-F]/

opcode: IDENTIFIER
decimal: DECIMAL
immediate: DECIMAL | HEXDECIMAL

regular_register: "R" NUMBER_Z
uniform_register: "UR" NUMBER_Z
register: regular_register | uniform_register

predicate: "P" NUMBER_T

predicate_negated: "!" predicate
predicate_neg_opt: predicate | predicate_negated

memory: "[" ((register ("+" immediate)?) | immediate) "]"
constant_memory: "c[" immediate "][" (register | immediate) "]"

operand: register | predicate | memory | constant_memory | immediate

instruction: control predicate_neg_opt? opcode (operand ("," operand)*)?

%ignore /\s/
